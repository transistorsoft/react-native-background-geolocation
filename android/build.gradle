apply plugin: 'com.android.library'

// Android dependencies
def DEFAULT_COMPILE_SDK_VERSION                     = 28
def DEFAULT_TARGET_SDK_VERSION                      = 28
def DEFAULT_APP_COMPAT_VERSION                      = "28.0.0"

// Plugin dependencies
def DEFAULT_GOOGLE_PLAY_SERVICES_LOCATION_VERSION   = "17.0.0"
def DEFAULT_OK_HTTP_VERSION                         = "3.12.1"
def DEFAULT_ANDROID_PERMISSIONS_VERSION             = "0.1.8"
def DEFAULT_EVENTBUS_VERSION                        = "3.0.0"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
}

repositories{
    maven {
        url './libs'
    }
}

dependencies {
    // Prefer appCompatVersion vs supportLibVersion.
    def appCompatVersion = safeExtGet('appCompatVersion', safeExtGet('supportLibVersion', DEFAULT_APP_COMPAT_VERSION))

    def appCompatMajorVersion = appCompatVersion.split('\\.')[0] as int
    def appCompatLibName =  (appCompatMajorVersion < 20) ? "androidx.appcompat:appcompat" : "com.android.support:appcompat-v7"
    def playServicesLocationVersion = safeExtGet('googlePlayServicesLocationVersion', DEFAULT_GOOGLE_PLAY_SERVICES_LOCATION_VERSION)
    def okHttpVersion = safeExtGet('okHttpVersion', DEFAULT_OK_HTTP_VERSION)
    def androidPermissionsVersion = safeExtGet('androidPermissionsVersion', DEFAULT_ANDROID_PERMISSIONS_VERSION)
    def eventBusVersion = safeExtGet('eventBusVersion', DEFAULT_EVENTBUS_VERSION)

    compileOnly 'com.facebook.react:react-native:+'
    // tslocationmanager.aar
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    implementation "$appCompatLibName:$appCompatVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesLocationVersion"
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.3.0-2'
    implementation "com.intentfilter:android-permissions:$androidPermissionsVersion"
}
